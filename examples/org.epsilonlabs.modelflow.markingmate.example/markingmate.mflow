param mBasedir : String;
param tBasedir : String;
param mm : String;

/* Models */

model SEPR is epsilon:emf {
	src: mBasedir + "sepr.model"
	metamodelFile: mm
}
model BOB is epsilon:emf {
	src: mBasedir + "sepr-bob.model"
	metamodelFile: mm
}			
model ALICE is epsilon:emf {
	src: mBasedir + "sepr-alice.model"
	metamodelFile: mm
}	
model MARKED is epsilon:emf {
	src: mBasedir + "sepr-marked.model"
	metamodelFile: mm
}

/* Tasks */

task validateSetup is epsilon:evl
	in SEPR 
{ 
	src: tBasedir + "validateSetup.evl"
	failOnError: true
}
task splitAlice is epsilon:etl 
	in SEPR
	out ALICE as Reviewer 
	dependsOn validateSetup 
{
	src: tBasedir + "split.etl"
	params {
		var m = new Map;
		m.put("questions", Sequence{0..1});
		return m;
	}
}
task splitBob is epsilon:etl 
	in SEPR
	out BOB as Reviewer 
	dependsOn validateSetup 
{
	src: tBasedir + "split.etl"
	params {
		var m2 = new Map;
		m2.put("questions", Sequence{2..3});
		return m2;
	}
}
task validateMarking is epsilon:evl 
	in ALICE as Left,Reviewer
		and BOB as Right,Reviewer {
	src: tBasedir + "validateMarking.evl"
	failOnError : true
}
@always
task compare is epsilon:ecl 
	in ALICE as Left and BOB as Right 
	dependsOn validateMarking 
{
	src: tBasedir + "markingmate.ecl"
}
task merge is epsilon:eml 
	in ALICE as Left, Source
		and BOB as Right, Source
		and compare.comparison as matchTrace
	out MARKED as Target
{
	src: tBasedir + "markingmate.eml"
}
task csv is epsilon:egx 
	in MARKED {
	src: tBasedir + "feedback.egx"
	outputRoot: "output"
}
post Done {
	"done".println();
}